######################################################
# LINK_dataset
######################################################

DATASET = "LINK_dataset"

RAW_DIR = config["RAW_DIR"]
PROCESSED_DIR = config["PROCESSED_DIR"]


checkpoint prepare_raw_data:
    output:
        f"{RAW_DIR}/{DATASET}/manifest.txt"
    shell:
        f"""
        find {RAW_DIR}/{DATASET}/ -type f -name "*.pkl" | sed "s|^{RAW_DIR}/{DATASET}/||" | sed "s|^/*||" | sed 's/_preprocess.pkl$//'> {{output}}
        
        """

rule prepare_data:
    input:
        pkl_file = f"{RAW_DIR}/{DATASET}/{{file}}"
    output:
        temp(f"{PROCESSED_DIR}/{DATASET}/tmp/{{file}}.txt")
    log:
        f".snakemake/logs/{DATASET}/prepare_data.{{file}}.log"
    shell:
        f"""
        mkdir -p {PROCESSED_DIR}/{DATASET}/tmp
        python -m brainsets_pipelines.{DATASET}.prepare_data --input_file {{input.pkl_file}} --output_dir {PROCESSED_DIR}/{DATASET} >> {{log}}
        find {PROCESSED_DIR}/{DATASET}/ -type f -name "*.h5" | sed "s|^{PROCESSED_DIR}/{DATASET}//||" > {{output}}
        """
def aggregate_input(wildcards):
    with checkpoints.prepare_raw_data.get(**wildcards).output[0].open() as manifest:
        files = [line.strip() for line in manifest]
    return expand(f"{PROCESSED_DIR}/{DATASET}/tmp/{{file}}.txt", file=files)


rule merge_manifests:
    input:
        aggregate_input
    output:
        f"{PROCESSED_DIR}/{DATASET}/manifest.txt"
    shell:
        f"""
        find {PROCESSED_DIR}/{DATASET}/ -type f -name "*.h5" | sed "s|^{PROCESSED_DIR}/{DATASET}//||" > {{output}}        
        """

rule all:
    input:
        f"{PROCESSED_DIR}/{DATASET}/manifest.txt"
